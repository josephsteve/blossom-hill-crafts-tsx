Query(
  Lambda(
    ["cart_total", "cart_details"],
    Let(
      {
        newTransData: Select(
          ["ref", "id"],
          Create(Collection("transactions"), {
            data: {
              trans_date: Select(["trans_date"], Var("cart_total")),
              transaction_date: Now(),
              total_items: Select(["total_items"], Var("cart_total")),
              detail_total: Select(["detail_total"], Var("cart_total")),
              tax_rate: Select(["tax_rate"], Var("cart_total")),
              tax_amount: Select(["tax_amount"], Var("cart_total")),
              total_price: Select(["total_price"], Var("cart_total"))
            }
          })
        )
      },
      Do(
        Foreach(
          Var("cart_details"),
          Lambda(
            "detail",
            Let({
              product_ref_id: Select(["product_ref_id"], Var("detail")),
              price_existing: Select(["data", "price_current"], Get(Ref(Collection("products"), Var("product_ref_id")))),
              price_current: Select(["price_current"], Var("detail"))
            },
            Do(
              If(
                Not(Equals(Var("price_existing"), Var("price_current"))),
                Create(Collection("pricing_log"), {
                  data: {
                    product: Ref(Collection("products"), Var("product_ref_id")),
                    price: Select(["price_current"], Var("detail")),
                    log_date: Now()
                  }
                }),
                null
              ),
              Create(Collection("transaction_details"), {
                data: {
                  transaction: Ref(
                    Collection("transactions"),
                    Var("newTransData")
                  ),
                  product: Ref(Collection("products"), Var("product_ref_id")),
                  product_id: Select(["product_id"], Var("detail")),
                  product_display_name: Select(
                    ["product_display_name"],
                    Var("detail")
                  ),
                  product_description: Select(
                    ["product_description"],
                    Var("detail")
                  ),
                  price_current: Var("price_existing"),
                  price_sell: Select(["price_sell"], Var("detail")),
                  supplier: Ref(
                    Collection("suppliers"),
                    Select(["supplier_ref_id"], Var("detail"))
                  ),
                  supplier_name: Select(["supplier_name"], Var("detail"))
                }
              }),
              Update(Ref(Collection("products"), Var("product_ref_id")), {
                data: {
                  status: "sold",
                }
              })
            )
            )            
          )
        )
      )
    )
  )
)